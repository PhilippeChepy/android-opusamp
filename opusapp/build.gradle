apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '20.0.0'

    defaultConfig {
        applicationId "net.opusapp.player.test"
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 15
        versionName "1.0.2.0"
    }

    flavorDimensions "abi", "version"

    productFlavors {
        premiumApp {
            flavorDimension "version"
            applicationId 'net.opusapp.player.premium'

//            android.sourceSets.premiumApp.manifest.srcFile 'src/flavor-premium/AndroidManifest.xml'
            android.sourceSets.premiumApp.java.srcDirs = ['src/flavor-premium/java']
            android.sourceSets.premiumApp.res.srcDirs = ['src/flavor-premium/res']
        }

        freeApp {
            flavorDimension "version"
            applicationId 'net.opusapp.player'

            android.sourceSets.freeApp.manifest.srcFile 'src/flavor-free/AndroidManifest.xml'
            android.sourceSets.freeApp.java.srcDirs = ['src/flavor-free/java']
            android.sourceSets.freeApp.res.srcDirs = ['src/flavor-free/res']
        }

        x86 {
            flavorDimension "abi"
            versionCode 4 * 1000000 + defaultConfig.versionCode
            ndk {
                abiFilter "x86"
            }
        }
        mips {
            flavorDimension "abi"
            versionCode 3 * 1000000 + defaultConfig.versionCode
            ndk {
                abiFilter "mips"
            }
        }
        armv7 {
            flavorDimension "abi"
            versionCode 2 * 1000000 + defaultConfig.versionCode
            ndk {
                abiFilter "armeabi-v7a"
            }
        }
        arm {
            flavorDimension "abi"
            versionCode 1 * 1000000 + defaultConfig.versionCode
            ndk {
                abiFilter "armeabi"
            }
        }
        fat {
            flavorDimension "abi"
        }
    }
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }
    signingConfigs {
        release {
            storeFile file("/mnt/net/devel/tools/android-keystore/keystore")
            storePassword "***REMOVED***"
            keyAlias "android"
            keyPassword "***REMOVED***"
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
        }
        debug {
            jniDebuggable true
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    /* UniversalImageLoader */
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.2'
    /* SlidingupPanel */
    compile 'com.sothree.slidinguppanel:library:2.0.4'
    /* PagerSLidingTabStrip */
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    /* DragSortListView - directly in source tree - no longer maintained, previously : compile 'com.mobeta.android.dslv:drag-sort-listview:0.6.1' */
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.android.support:support-v4:21.0.0'

    freeAppCompile 'com.google.android.gms:play-services:6.1.71'
}

