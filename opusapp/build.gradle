apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '20.0.0'

    defaultConfig {
        applicationId "net.opusapp.player"
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 4
        versionName "1.0.0.4"
    }

    flavorGroups "abi", "version"

    productFlavors {
        premiumApp {
            flavorGroup "version"
            packageName 'net.opusapp.player.premium'

            android.sourceSets.premiumApp.java.srcDirs = ['src/flavor-premium/java']
            android.sourceSets.premiumApp.res.srcDirs = ['src/flavor-premium/res']
        }

        freeApp {
            flavorGroup "version"
            packageName 'net.opusapp.player'

            android.sourceSets.freeApp.java.srcDirs = ['src/flavor-free/java']
            android.sourceSets.freeApp.res.srcDirs = ['src/flavor-free/res']
        }

        x86 {
            flavorGroup "abi"
            versionCode 4 * 1000000 + defaultConfig.versionCode
            ndk {
                abiFilter "x86"
            }
        }
        mips {
            flavorGroup "abi"
            versionCode 3 * 1000000 + defaultConfig.versionCode
            ndk {
                abiFilter "mips"
            }
        }
        armv7 {
            flavorGroup "abi"
            versionCode 2 * 1000000 + defaultConfig.versionCode
            ndk {
                abiFilter "armeabi-v7a"
            }
        }
        arm {
            flavorGroup "abi"
            versionCode 1 * 1000000 + defaultConfig.versionCode
            ndk {
                abiFilter "armeabi"
            }
        }
        fat {
            flavorGroup "abi"
        }
    }
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }
    signingConfigs {
        release {
            storeFile file("/mnt/net/devel/tools/android-keystore/keystore")
            storePassword "***REMOVED***"
            keyAlias "android"
            keyPassword "***REMOVED***"
        }
    }
    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            jniDebugBuild true
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    /* UniversalImageLoader */
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.2'
    /* SlidingupPanel */
    compile 'com.sothree.slidinguppanel:library:+'
    /* PagerSLidingTabStrip */
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    /* DragSortListView - directly in source tree - no longer maintained, previously : compile 'com.mobeta.android.dslv:drag-sort-listview:0.6.1' */
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.android.support:support-v4:21.+'

    freeAppCompile 'com.google.android.gms:play-services:6.1.11'
    freeAppCompile project(':opuslicensing');

}

